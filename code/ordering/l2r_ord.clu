#extend

% The procedures "l2r_quiet", "l2r_user", and "l2r_auto" in this file comprise
% the "l2r_ordering", which orders equations from left to right.


% Orders "eq" from left to right if the result is a valid rewrite rule whose 
% left side is neither "true" nor "false".

l2r_quiet = proc (reg: registry, eq: equation) returns (rewrite_rule)
	      signals (cannot_orient)
    return(equation$e2cr(eq))
       except when impossible: end
    return(equation$e2r(eq))
       except when invalid_rule: signal cannot_orient end
    end l2r_quiet


% Attempts to order "eq" using "l2r_quiet".  If this fails, presents an
% appropriate list of choices for ordering "eq" to the user (see the
% "ordering_choice" procedure).

l2r_user = proc (p: printer, reg: registry, stab: symtab, eq: equation)
	     returns (order_action)
    own sug: suggestions := suggestions$[]
    tracer$present_equation(eq)
    begin
	rr: rewrite_rule := l2r_quiet(reg, eq)
	printer$text(p, "\nOrdered automatically.\n")
	return(order_action$make_auto(rr))
	end except when cannot_orient: end
    return(ordering_choice[suggestions]
	       (eq, reg, p, stab, sug, sug, false, false, dsmpos_choice))
    end l2r_user


% If "choice" is greater than one, signals "cannot_orient" (because there's no
% point in backing up).  Otherwise, attempts to order "eq" using "l2r_quiet".

l2r_auto = proc (reg: registry, eq: equation, choice: int)
	     returns (rewrite_rule) signals (cannot_orient)
    if choice > 1 then signal cannot_orient end
    return(l2r_quiet(reg, eq))
       resignal cannot_orient
    end l2r_auto
