% Facts about finite sets

set name set

declare sorts Elem, Set
declare variables e, e': Elem, x, y, z: Set
declare operators
  {}:                      -> Set
  insert:        Elem, Set -> Set
  {__}:          Elem      -> Set
  __\union__:    Set, Set  -> Set
  __\in__:       Elem, Set -> Bool
  __\subseteq__: Set, Set  -> Bool
  ..


% Axioms

assert 
  sort Set generated by {}, insert;
  sort Set partitioned by \in;
  ac \union;
  {e} = insert(e, {});
  {} \union x = x;
  insert(e, x) \union y = insert(e, x \union y);
  ~(e \in {});
  e \in insert(e', x) <=> e = e' \/ e \in x;
  {} \subseteq x;
  insert(e, x) \subseteq y <=> e \in y /\ x \subseteq y
  ..


% Theorems (see ~lp/examples/proofs/set.lp for proofs)

set name setInductionRule
assert sort Set generated by {}, {__}, \union

set name setTheorem
assert
  e \in (x \union y) <=> e \in x \/ e \in y;
  x \union x = x;
  insert(e, insert(e, x)) = insert(e, x);
  e \in x /\ x \subseteq y => e \in y;
  (x \union y) \subseteq z <=> x \subseteq z /\ y \subseteq z;
  x \subseteq (x \union y);
  x \subseteq x;
  x \subseteq y /\ y \subseteq z => x \subseteq z
  ..
